# nix/packages.nix — Hermes Agent package built with uv2nix
{ inputs, ... }: {
  perSystem = { pkgs, system, ... }:
    let
      hermesVenv = pkgs.callPackage ./python.nix {
        inherit (inputs) uv2nix pyproject-nix pyproject-build-systems;
      };

      runtimeDeps = with pkgs; [
        nodejs_20 ripgrep git openssh ffmpeg
      ];

      runtimePath = pkgs.lib.makeBinPath runtimeDeps;
    in {
      packages.default = pkgs.stdenv.mkDerivation {
        pname = "hermes-agent";
        version = "0.1.0";

        dontUnpack = true;
        dontBuild = true;
        nativeBuildInputs = [ pkgs.makeWrapper ];

        installPhase = ''
          runHook preInstall

          # Wrap entry points from the uv2nix venv
          mkdir -p $out/bin
          makeWrapper ${hermesVenv}/bin/hermes $out/bin/hermes \
            --prefix PATH : "${runtimePath}"

          makeWrapper ${hermesVenv}/bin/hermes-agent $out/bin/hermes-agent \
            --prefix PATH : "${runtimePath}"

          makeWrapper ${hermesVenv}/bin/hermes-acp $out/bin/hermes-acp \
            --prefix PATH : "${runtimePath}"

          runHook postInstall
        '';

        meta = with pkgs.lib; {
          description = "AI agent with advanced tool-calling capabilities";
          homepage = "https://github.com/NousResearch/hermes-agent";
          mainProgram = "hermes";
          license = licenses.mit;
          platforms = platforms.unix;
        };
      };
    };
}
